natural_newline = _{"␍␊" | "\r\n" | "\n"}
WHITESPACE = _{ " " | "\t" }

identifier = @{ "_"* ~ ASCII_ALPHANUMERIC ~ (ASCII_ALPHANUMERIC | "_" )* }
register = @{ "reg" ~ ASCII_DIGIT+ }

unsigned = @{ ASCII_DIGIT+ }
signed = @{ "-"? ~ ASCII_DIGIT+ }
float = @{ signed ~ "." ~ unsigned }

value = _{ float | unsigned | signed  | register }
literals = {float | unsigned | signed}

operation_type = { "u" | "s" | "f" }



label = { identifier ~ ":" }

move_ = {
    register ~ "=" ~ (register | (operation_type ~ literals))
}

equal = {
    register ~ "=" ~ register ~ "==" ~ value
}

not_equal = {
    register ~ "=" ~ register ~ "!=" ~ value
}

not = {
    register ~ "=" ~ "!" ~ value
}


and = {
    register ~ "=" ~ register ~ "&&" ~ value
}

or = {
    register ~ "=" ~ register ~ "||" ~ value
}

xor = {
    register ~ "=" ~ register ~ "^^" ~ value
}

shift_left = {
    register ~ "=" ~ register ~ "<<" ~ value
}

shift_right = {
    register ~ "=" ~ register ~ ">>" ~ value
}

jump = {
    "jmp" ~ identifier
}

jnz = {
    "jnz" ~ identifier
}

push = {
    "push" ~ value
}

pop = {
    "pop" ~ register
}

call = {
    "call" ~ (identifier | unsigned)
}

ret = {
    "ret"
}

syscall = {
    "syscall" ~ (identifier | unsigned)
}

store = {
    (unsigned | register) ~ "<-" ~ register ~ ("0" | "2" | "3" )?
}

load = {
    register ~ "<-" ~ (unsigned | register) ~ ("0" | "2" | "3" )?
}

add = {
    register ~ "=" ~ operation_type ~ register ~ "+" ~ value
}

sub = {
    register ~ "=" ~ operation_type ~ register ~ "-" ~ value
}

mul = {
    register ~ "=" ~ operation_type ~ register ~ "*" ~ value
}

div = {
    register ~ "=" ~ operation_type ~ register ~ "/" ~ value
}

mod_ = {
    register ~ "=" ~ operation_type ~ register ~ "%" ~ value
}

divmod = {
    register ~ "," ~ register ~ "=" ~ operation_type ~ register ~ "/%" ~ value
}

greater_than = {
    register ~ "=" ~ operation_type ~ register ~ ">" ~ value
}

less_than = {
    register ~ "=" ~ operation_type ~ register ~ "<" ~ value
}

greater_than_or_equal = {
    register ~ "=" ~ operation_type ~ register ~ ">=" ~ value
}

less_than_or_equal = {
    register ~ "=" ~ operation_type ~ register ~ "<=" ~ value
}

negate = {
    register ~ "=" ~ operation_type ~ "-" ~ value
}







statement = _{
    natural_newline* ~ (
        move_ | equal | not_equal | not | and | or | xor | shift_left |
        shift_right | jump | jnz | push | pop | call | ret | syscall |
        store | load | add | sub | mul | div | mod_ | divmod | greater_than |
        less_than | greater_than_or_equal | less_than_or_equal | negate
    ) ~ natural_newline*
}



program = _{ SOI ~ statement* ~ EOI}